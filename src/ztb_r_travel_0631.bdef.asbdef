managed implementation in class zbp_tb_r_travel_0631 unique;
strict ( 2 );

define behavior for ZTB_R_TRAVEL_0631 alias Travel
persistent table ztb_travel_0631
lock master
authorization master ( instance )
etag master LocalLastChangedAt
{

  create ( authorization : global );
  update;
  delete;

  field ( readonly ) TravelID, LocalLastChangedAt, LastChangedAt, LastChangedBy, CreatedAt, CreatedBy;
  field ( mandatory ) AgencyID, OverallStatus, BookingFee, CurrencyCode;
  field ( features : instance ) BookingFee;

  validation validateCustomer on save { field CustomerID; }
  validation validateDates on save { field BeginDate, EndDate; }
  validation validateStatus on save { field OverallStatus; }

  action ( features : instance ) acceptTravel result [1] $self;
  action ( features : instance ) rejectTravel result [1] $self;
  action createTravelByTemplate result [1] $self;

  association _Booking { create; }

  mapping for ztb_travel_0631
    {
      TravelID           = travel_id;
      AgencyID           = agency_id;
      CustomerID         = customer_id;
      BeginDate          = begin_date;
      EndDate            = end_date;
      BookingFee         = booking_fee;
      TotalPrice         = total_price;
      CurrencyCode       = currency_code;
      Description        = description;
      OverallStatus      = overall_status;
      CreatedBy          = created_by;
      CreatedAt          = created_at;
      LastChangedBy      = last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }

}

define behavior for ZTB_R_BOOKING_0631 alias Booking
implementation in class zbp_tb_r_booking_0631 unique
persistent table ztb_booking_0631
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;

  field ( readonly ) TravelID, BookingID;
  field ( mandatory ) AirlineID, ConnectionId, FlightDate, BookingStatus;

  validation validateStatus on save { field BookingStatus; }

  determination calculateTotalFlightPrice on modify { field FlightPrice , CurrencyCode; }

  association _Travel;
  association _BookingSuppl { create; }

  mapping for ztb_booking_0631
    {
      TravelID           = travel_id;
      BookingID          = booking_id;
      BookingDate        = booking_date;
      CustomerID         = customer_id;
      AirlineID          = carrier_id;
      ConnectionId       = connection_id;
      FlightDate         = flight_date;
      FlightPrice        = flight_price;
      CurrencyCode       = currency_code;
      BookingStatus      = booking_status;
      LocalLastChangedAt = local_last_changed_at;
    }

}

define behavior for ZTB_R_BOOKSUPP_631 alias Supplements
implementation in class zbp_tb_r_booksupp_631 unique
//persistent table ztb_booksupp_631
with unmanaged save
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;

  field ( readonly ) TravelID, BookingID, BookingSupplementID;

  field ( mandatory ) SupplementID, Price;

  determination calculateTotalSupplimPrice on modify { field Price , CurrencyCode; }

  association _Travel;
  association _Booking;

  mapping for ztb_booksupp_631
    {
      TravelID            = travel_id;
      BookingID           = booking_id;
      BookingSupplementID = booking_supplement_id;
      SupplementID        = supplement_id;
      Price               = price;
      CurrencyCode        = currency_code;
      LocalLastChangedAt  = local_last_changed_at;
    }

}